# system level imports
import os
import sys

# add scripts to python path for utility functions
sys.path.append('../../scripts/python')
from utils import link_ids_to_input

# retrieve config file
configfile: '../../files/config.yaml'

# set global parameter values
END_KEYS = sorted(list(config['end_denote'].keys()))
DIRNAMES = link_ids_to_input(config['dirs']['data'],
                             config['sample_regex']['id'])
rule all:
    input:
        expand(os.path.join(config['dirs']['output'], 'qc', '{sample}',
                            '{sample}_{end}_qc.fastq.gz'),
               sample=list(DIRNAMES.keys()), end=config['end_denote'])
        
# combine lanes for each read direction
rule fastq_combine:
    input:
        lambda wildcards: DIRNAMES[wildcards.sample]
    output:
        # temporary because we'll align to filtered data
        temp(os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                          '{sample}_{end}.fastq.gz'))
    shell:
        'cat {input}/{wildcards.sample}*{wildcards.end}*.fastq.gz >> {output}'


# AfterQC with fastp
rule fastp_qc:
    input:
        r1=os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                        '{sample}' + '_{}.fastq.gz'.format(END_KEYS[0])),
        r2=os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                        '{sample}' + '_{}.fastq.gz'.format(END_KEYS[1]))
    log:
        os.path.join(config['dirs']['log'], 'fastp/{sample}.log')
    params:
        p1=config['params']['fastp']
    output:
        r1=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                        '{sample}' + '_{}_qc.fastq.gz'.format(END_KEYS[0])),
        r2=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                        '{sample}' + '_{}_qc.fastq.gz'.format(END_KEYS[1])),
        html=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                          'fastp.html'),
        json=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                          'fastp.json')
    shell:
        'source activate qc;'
        '(fastp {params.p1} -i {input.r1} -I {input.r2} -o {output.r1} -O '
        '{output.r2} -h {output.html} -j {output.json}) 2> {log}'
