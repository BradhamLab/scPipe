# system level imports
shell.prefix('conda activate alignment; ')
import os
import sys

# add scripts to python path for utility functions
sys.path.append('../../scripts/python')
import utils

# retrieve config file
configfile: '../../files/config.yaml'
config = utils.configure_run(config)

# set global parameter values
END_KEYS = sorted(list(config['dataset']['ends'].keys()))
DIRNAMES = utils.link_ids_to_input(config['dirs']['data'],
                                   config['sample_regex']['id'])

def find_extension(fq):
    first, ext = os.path.splitext(fq)
    if ext != '.gz':
        raise IOError("Expected gzipped fastq file")
    return os.path.splitext(first)[1].replace('.', '')
    
fq_ext = find_extension(os.listdir(list(DIRNAMES.values())[0])[0])

rule all:
    input:
        expand(os.path.join(config['dirs']['output'], 'qc', '{sample}',
                            '{sample}_{end}_qc.fastq.gz'),
               sample=list(DIRNAMES.keys()), end=END_KEYS)
        
# combine lanes for each read direction
rule fastq_combine:
    input:
        lambda wildcards: DIRNAMES[wildcards.sample]
    output:
        # temporary because we'll align to filtered data
        temp(os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                          '{sample}_{end}.fastq.gz'))
    params:
        fastq=fq_ext
    shell:
        'cat {input}/{wildcards.sample}*{wildcards.end}*.{params.fastq}.gz >> {output}'


# AfterQC with fastp
rule fastp_qc:
    input:
        r1=os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                        '{sample}' + '_{}.fastq.gz'.format(END_KEYS[0])),
        r2=os.path.join(config['dirs']['output'], 'fastq', '{sample}',
                        '{sample}' + '_{}.fastq.gz'.format(END_KEYS[1]))
    log:
        os.path.join(config['dirs']['log'], 'fastp/{sample}.log')
    params:
        p1=config['params']['fastp']
    output:
        r1=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                        '{sample}' + '_{}_qc.fastq.gz'.format(END_KEYS[0])),
        r2=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                        '{sample}' + '_{}_qc.fastq.gz'.format(END_KEYS[1])),
        html=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                          'fastp.html'),
        json=os.path.join(config['dirs']['output'], 'qc', '{sample}',
                          'fastp.json')
    shell:
        '(fastp {params.p1} -i {input.r1} -I {input.r2} -o {output.r1} -O '
        '{output.r2} -h {output.html} -j {output.json}) 2> {log}'
