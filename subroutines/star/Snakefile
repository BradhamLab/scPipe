import subprocess as sbp
import numpy as np

configfile: '../../files/config.yaml'

def link_sample_dirs(data_dir, sample_regex):
    """
    Link sample ids to fastq containing folders.

    Links sampel ids to sample-specific folders containing raw .fastq.gz
    folders. 

    Args:
        data_dir (string): parent directory containing all sample-specific
            directories.
        sample_regex (string): regex pattern to extract sample ids from
            directory names.
    Returns:
        (dict, string): dictionary mapping sample ids to data directories. 
        
    """
    id_to_dir = {}
    sample_pattern = re.compile(sample_regex)
    for sample_dir in os.listdir(data_dir):
        matched = re.search(sample_pattern, sample_dir)
        if matched is not None:
            sample_id = sample_dir[0:matched.span()[0]]
            data_loc = os.path.join(data_dir, sample_dir)
            id_to_dir[sample_id] = data_loc
    return id_to_dir


# function to get genomeChrBinNBits parameter for STAR alignment.
def estimate_STAR_ChrBinNbits(genome_file, read_length):
    """
    Estimate the `ChrBinNBits` parameter for genome indexing in STAR

    Estimate the `ChrBinNBits` parameter for genome indexing in STAR. Value
    must be estimated due to memory constraints caused by the large number
    of scaffolds present in some genomes (i.e. the LV genome). If estimation
    is unnecessary, flag `star_est_ChrBinNbits: False` in configuration file.

    Args:
        genome_file (string): path to fasta file containing genome reference
            sequences.
        read_length (int): length of reads from RNAseq experiment.

    Return:
        (int) new value for scaling RAM consumption

    References:
    https://github.com/alexdobin/STAR/blob/master/doc/STARmanual.pdf (p. 7)
    https://github.com/alexdobin/STAR/issues/103
    """
    len_call = 'grep -v ">" {} | wc | awk '.format(genome_file)\
               + "'{print $3-$1}'"
    n_ref_call = 'grep "^>" {} | wc -l'.format(genome_file)

    return_values = [None, None]
    for i, call in enumerate([len_call, n_ref_call]):
        p = sbp.Popen(call, stdin=sbp.PIPE, stdout=sbp.PIPE, stderr=sbp.PIPE,
                      shell=True)
        output, err = p.communicate()
        if p.returncode == 0:
            return_values[i] = int(output.strip())
        else:
            raise OSError(err)
    estimate = max([int(np.log2(return_values[0] / return_values[1])),
                    int(np.log2(read_length))])
    return min(18, estimate)


def get_star_genome_params(config_dict):
    """
    Extract parameters for genome indexing in STAR.

    Args:
        config_dict (dictionary): configuration dictionary created by snakemake
            via configfile: {file.name}
    Returns:
        (string): string of arguments to pass STAR.
    """

    star_genome_params = config_dict['star_genome_params']
    if config_dict['star_est_ChrBinsNbits'] == True:
        nbits = estimate_STAR_ChrBinNbits(config['genome_fasta'],
                                        config['read_length'])
        star_genome_params += ' --genomeChrBinNbits {}'.format(nbits)
    return star_genome_params

OUTPUT = config['output_dir']
LOGS = config['log_dir']
ENDS = config['end_denote']
READ_ENDS = sorted(list(ENDS.keys()))
DATA_DIR = config['data_dir']
SAMPLE_REGEX = config['sample_regex']
DIRNAMES = link_sample_dirs(DATA_DIR, SAMPLE_REGEX)
IDS = list(DIRNAMES.keys())

rule all:
    input:
        expand(os.path.join(OUTPUT, 'star', 'sorted_alignments', '{sample}',
                            '{sample}_sorted.bam'), sample=IDS)

# Align with star
rule star_generate_genome:
    input:
        gtf=config['gtf'],
        fasta=config['genome_fasta']
    params:
        read_length=(config['read_length'] - 1),
        extra=get_star_genome_params(config),
        log=os.path.join(LOGS, 'star')
    log:
        os.path.join(LOGS, 'star', 'star_genome.log')
    output:
        genome=protected(directory(os.path.join(OUTPUT, config['genome_dir'])))
    shell:
        'mkdir {output.genome}; (STAR --runMode genomeGenerate '
        '--genomeDir {output.genome} --genomeFastaFiles {input.fasta} '
        '--sjdbGTFfile {input.gtf} --sjdbOverhang {params.read_length} '
        '--outFileNamePrefix {params.log} {params.extra}) 2> {log}'

# align reads
rule star_align_reads:
    input:
        r1=os.path.join(OUTPUT, 'qc/{sample}/{sample}_R1_qc.fastq.gz'),
        r2=os.path.join(OUTPUT, 'qc/{sample}/{sample}_R2_qc.fastq.gz'),
        genome=os.path.join(config['genome_dir'])
    params:
        prefix=os.path.join(OUTPUT, 'star', 'alignments', '{sample}',
                            '{sample}'),
        extra=config['star_align_params']
    log:
        os.path.join(LOGS, 'star', '{sample}', 'star_alignment.log')
    output:
        temp(os.path.join(OUTPUT, 'star', 'alignments', '{sample}',
                          '{sample}Aligned.out.bam'))
    shell:
        '(STAR --runMode alignReads --outSAMtype BAM Unsorted '
        '--readFilesCommand zcat --genomeDir {input.genome} '
        '--outFileNamePrefix {params.prefix} '
        '--readFilesIn {input.r1} {input.r2} {params.extra}) 2> {log}'

# sort alignments b/c paired-end reads -- required by HTSeq
rule sort_alignments:
    input:
        os.path.join(OUTPUT, 'star', 'alignments', '{sample}',
                     '{sample}Aligned.out.bam')
    output:
        os.path.join(OUTPUT, 'star', 'sorted_alignments', '{sample}',
                     '{sample}_sorted.bam')
    shell:
        "samtools sort -n {input} -o {output}"
